plugins {
    id 'java'
}

repositories {
    maven {
        url 'https://packages.atlassian.com/maven/repository/public/'
    }
}

def elasticBambooVersion = '8.4'
version = '1.0.188'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

configurations {
    elasticImage
}

dependencies {
    implementation "com.atlassian.aws:atlassian-aws-bootstrap:${version}"

    elasticImage group: 'com.atlassian.bamboo', name: 'atlassian-bamboo-elastic-image', version: elasticBambooVersion, ext: 'zip'
}

jar {
    archiveBaseName = 'atlassian-aws-bootstrap'
    from {
        zipTree(configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts[0].file)
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

def installerJarName = 'atlassian-bamboo-agent-elastic-installer'

task installerJar(type: Jar, dependsOn: jar) {
    archiveBaseName = installerJarName
    archiveVersion = ''
    manifest {
        attributes 'Main-Class': 'com.atlassian.bamboo.agent.elastic.installer.ElasticAgentInstaller'
    }
    from { zipTree(jar.outputs.files[0]) }
    from {
        zipTree(
            zipTree(configurations.elasticImage.resolvedConfiguration.resolvedArtifacts[0].file) \
                .matching { include 'bin/*.jar' }[0])
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task installerZip(type: Zip, dependsOn: installerJar) {
    archiveBaseName = 'atlassian-bamboo-elastic-image'
    archiveVersion = elasticBambooVersion
    from(installerJar.outputs) { into 'bin' }
    from(zipTree(configurations.elasticImage.resolvedConfiguration.resolvedArtifacts[0].file)) {
        exclude "bin/${installerJarName}.jar"
    }
}

assemble.dependsOn installerZip
